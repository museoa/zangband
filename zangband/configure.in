dnl Process this file with autoconf to produce a configure script.

dnl Initialize autoconf - look for the existance of a file to make sure
dnl the source distribution is installed correctly.
AC_INIT(src/main.c)

dnl write the resulting #defines into a header file
AM_CONFIG_HEADER(src/autoconf.h)

AM_INIT_AUTOMAKE(zangband, 2.7.2cvs)

AC_ARG_WITH(setgid,
[  --with-setgid=NAME      setgid usage: install angband as group NAME],
[case "${withval}" in
  no) GAMEGROUP="";;
  yes) AC_MSG_ERROR(missing argument for --with-setgid);;
  *) GAMEGROUP="$withval";;
esac])
AC_SUBST(GAMEGROUP)
AM_CONDITIONAL(SET_GID, test "$GAMEGROUP" != "")

AC_LANG_C

basedir=`pwd`

dnl generate the installation path for the ./lib/ folder
if test "$GAMEGROUP" != ""; then
  game_datadir="$datadir/games/$PACKAGE/lib/"
else
  game_datadir="$basedir/lib/"
  bindir="$basedir"
fi
AC_DEFINE_UNQUOTED(DEFAULT_PATH, "$game_datadir")
DEFAULT_PATH="$game_datadir"
AC_SUBST(DEFAULT_PATH)

dnl Checks for programs.
AC_PROG_CC
AC_CHECK_TOOL(RANLIB, ranlib)

dnl -fno-strength-reduce prevents a bug in some versions of gcc
if test "$GCC" = yes; then
  CFLAGS="$CFLAGS -fno-strength-reduce"
fi


dnl Checks for libraries.

AC_CYGWIN
AC_MINGW32
AC_EXEEXT

AM_CONDITIONAL(CYGWIN, test "$CYGWIN" = "yes")

if test "$CYGWIN" = "yes"; then
  AC_DEFINE(WINDOWS)
  LIBS="$LIBS -lwinmm"
  LDFLAGS="$LDFLAGS -s -mwindows -e _mainCRTStartup"
  ANGBAND_OBJS="readdib.o angband.res"
  AC_SUBST(ANGBAND_OBJS)
else
  dnl Check for ncurses, curses, or termcap
  AC_CHECK_LIB(ncurses, initscr, [AC_DEFINE(USE_GCU) AC_DEFINE(USE_NCURSES) LIBS="$LIBS -lncurses"])
  if test "$ac_cv_lib_ncurses_initscr" != yes; then
    AC_CHECK_LIB(curses, initscr, [AC_DEFINE(USE_GCU) LIBS="$LIBS -lcurses"])
    if test "$ac_cv_lib_curses_initscr" != yes; then
      AC_CHECK_LIB(termcap, tgetent, [AC_DEFINE(USE_CAP) LIBS="$LIBS -ltermcap"])
    fi
  fi

  dnl Checks for header files.
  AC_PATH_XTRA

  if test "$no_x" != "yes"; then
    LIBS="$LIBS -lX11"
    AC_DEFINE(USE_X11)
    AC_DEFINE(USE_XPJ)
    ANGBAND_OBJS="maid-x11.o"
    AC_SUBST(ANGBAND_OBJS)
  fi

  LIBS="$X_PRE_LIBS $X_LIBS $X_EXTRA_LIBS $LIBS"
  CFLAGS="$X_CFLAGS $CFLAGS"
  LDFLAGS="$X_LIBS $LDFLAGS"

  dnl Check for the Athena widget set
  AC_CHECK_LIB(Xaw, XawInitializeWidgetSet, [AC_DEFINE(USE_XAW) LIBS="$LIBS -lXaw"])
fi

AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h strings.h sys/file.h sys/ioctl.h \
sys/time.h termio.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_FUNC_MEMCMP
AC_TYPE_SIGNAL
AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(gethostname mkdir select strstr strtol usleep mkstemp)

AC_OUTPUT(Makefile src/makefile src/lua/makefile lib/Makefile lib/apex/Makefile lib/bone/Makefile lib/data/Makefile lib/edit/Makefile lib/file/Makefile lib/help/Makefile lib/info/Makefile lib/pref/Makefile lib/save/Makefile lib/script/Makefile lib/user/Makefile lib/xtra/Makefile)
